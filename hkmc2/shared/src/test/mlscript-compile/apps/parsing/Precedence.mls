import "../../Option.mls"
import "../../Predef.mls"
import "../../BetterMap.mls"
import "./Keyword.mls"

open Option { Some, None }
open Predef { tuple }
open BetterMap
open Keyword { Keyword, INT_MIN, INT_MAX }

//  ____                        _
// |  _ \ _ __ ___  ___ ___  __| | ___ _ __   ___ ___
// | |_) | '__/ _ \/ __/ _ \/ _` |/ _ \ '_ \ / __/ _ \
// |  __/| | |  __/ (_|  __/ (_| |  __/ | | | (_|  __/
// |_|   |_|  \___|\___\___|\__,_|\___|_| |_|\___\___|
//
// ===================================================

module Precedence with ...

fun makePrecMap(startPrec: Int, ...ops) =
  let
    m = new Map
    i = 0
  while i < ops.length do
    ops.at(i).split(" ").forEach of (op, _, _) =>
      if op.length > 0 do
        m.insert of op, i + startPrec
    set i += 1
  m

module Keywords with
  val all = new Map()
  fun keyword(name, leftPrec, rightPrec) =
    let result = Keyword(name, leftPrec, rightPrec)
    all.insert(name, result)
    result
  let prec = 0 
  fun currPrec = Some(prec)
  fun nextPrec =
    set prec = prec + 1
    Some(prec)
  let basePrec = currPrec // the lowest precedence
  val _terminator = keyword(";;", basePrec, basePrec)
  val _class = keyword("class", None, basePrec)
  let semiPrec = nextPrec
  let commaPrec = nextPrec
  val _semicolon = keyword(";", semiPrec, basePrec)
  val _comma = keyword(",", commaPrec, semiPrec)
  let eqPrec = nextPrec
  let ascPrec = nextPrec
  val _equal = keyword("=", eqPrec, eqPrec)
  val _and = keyword("and", None, currPrec)
  val _bar = keyword of "|", None, currPrec
  val _thinArrow = keyword of "->", nextPrec, eqPrec
  val _colon = keyword(":", ascPrec, eqPrec)
  val _match = keyword("match", nextPrec, currPrec)
  val _while = keyword of "while", nextPrec, currPrec
  val _for = keyword of "for", nextPrec, currPrec
  val _to = keyword of "to", None, None
  val _downto = keyword of "downto", None, None
  val _do = keyword of "do", None, None
  val _done = keyword of "done", None, None
  val _of = keyword of "of", None, None
  val _with = keyword("with", None, currPrec)
  val _case = keyword("case", None, currPrec)
  let thenPrec = nextPrec
  val _if = keyword("if", nextPrec, thenPrec)
  val _leftArrow = keyword of "<-", thenPrec, thenPrec
  val _then = keyword("then", thenPrec, thenPrec)
  val _else = keyword("else", thenPrec, thenPrec)
  val _let = keyword("let", eqPrec, semiPrec)
  val _in = keyword("in", thenPrec, thenPrec)
  val _true = keyword("true", None, None)
  val _false = keyword("false", None, None)
  // val _fatArrow = keyword of "=>", nextPrec, eqPrec
  val _as = keyword("as", nextPrec, currPrec)
  val _fun = keyword("fun", currPrec, _thinArrow.leftPrec)
  val _function = keyword("function", currPrec, eqPrec)
  val _type = keyword("type", currPrec, None)
  val _exception = keyword("exception", currPrec, None)
  val _rec = keyword("rec", currPrec, eqPrec)
  val maxKeywordPrec = prec

  let precMap = makePrecMap of
    maxKeywordPrec
    ","
    "@"
    ":"
    "|"
    "&"
    "="
    "/ \\"
    "^"
    "!"
    "< >"
    "+ -"
    "* %"
    "~"
    "" // perfix operators
    "" // applications
    "."

  // The largest precedence value of all operators.
  val periodPrec = precMap.get(".") Option.unsafe.get()
  
  val _period = keyword of ".", Some(periodPrec), Some(periodPrec)
  
  val maxOperatorPrec = periodPrec
  val appPrec = maxOperatorPrec - 1
  val prefixPrec = appPrec - 1
  
  // Get the precedence of a single character operator.
  fun charPrec(op) = precMap.get(op) Option.unsafe.get()
  
  fun charPrecOpt(op) = precMap.get(op)
  
  val _equalequal = keyword of "==", charPrecOpt("="), charPrecOpt("=")
  
  let bracketPrec = Some(maxOperatorPrec + 1)
  
  val _leftRound = keyword of "(", bracketPrec, basePrec
  val _rightRound = keyword of ")", basePrec, None
  val _leftSquare = keyword of "[", bracketPrec, basePrec
  val _rightSquare = keyword of "]", basePrec, None
  val _leftCurly = keyword of "{", bracketPrec, basePrec
  val _rightCurly = keyword of "}", basePrec, None
  val _begin = keyword of "begin", bracketPrec, basePrec
  val _end = keyword of "end", basePrec, None

pattern Letter = "a" ..= "z" | "A" ..= "Z"

fun hasLetter(s) = [..s].some((ch, _, _) => ch is Letter)

pattern FloatOperator = "+." | "-." | "*." | "/."

pattern RightAssociative = "@" | "/" | "," | ":"

fun opPrec(opStr) = if
  opStr is FloatOperator then tuple of
    Keywords.charPrec of opStr.at(0)
    Keywords.charPrec of opStr.at(0)
  opStr hasLetter() then tuple of
    Keywords.maxKeywordPrec
    Keywords.maxKeywordPrec
  let lastChar = opStr.at(-1)
  let rightPrec = Keywords.charPrec of lastChar
  else tuple of
    Keywords.charPrec of opStr.at(0)
    (Keywords.charPrec of lastChar) +
      if lastChar is RightAssociative then -1 else 0

fun opPrecOpt(opStr) = if
  opStr is "" then None
  opStr is FloatOperator then Some of tuple of
    Keywords.charPrec of opStr.at(0)
    Keywords.charPrec of opStr.at(0)
  opStr hasLetter() then Some of tuple of
    Keywords.maxKeywordPrec
    Keywords.maxKeywordPrec
  let lastChar = opStr.at(-1)
  Keywords.charPrecOpt(lastChar) is Some(rightPrec) and
    Keywords.charPrecOpt(opStr.at(0)) is Some(leftPrec) then Some of tuple of
      leftPrec
      rightPrec + (if lastChar is RightAssociative then -1 else 0)
  else None
module TypeKeywords with
  // TODO: BEGIN deduplicate with Keywords (maybe use class extension?)
  val all = new Map()
  fun keyword(name, leftPrec, rightPrec) =
    let result = Keyword(name, leftPrec, rightPrec)
    all.insert(name, result)
    result
  let prec = 0
  fun currPrec = Some(prec)
  fun nextPrec =
    set prec = prec + 1
    Some(prec)
  // TODO: END deduplicate with Keywords (maybe use class extension?)
  
  let basePrec = currPrec
  
  // * These keywords basically only serve as stop signs.
  val _type = keyword("type", None, None)
  val _exception = keyword("exception", None, None)
  val _let = keyword of "let", None, None
  val _and = keyword of "and", None, None
  
  // * The semicolon is right associative.
  let semiPrec = currPrec
  val _semicolon = keyword(";", nextPrec, semiPrec)
  
  val _comma = keyword(",", nextPrec, currPrec) // * Only used for separating type parameters
  val _bar = keyword("|", None, None)
  val _as = keyword("", nextPrec, currPrec)
  let arrowPrec = currPrec
  val _arrow = keyword("->", nextPrec, arrowPrec)
  val _asterisk = keyword("*", nextPrec, currPrec)
  val _colon = keyword(":", nextPrec, currPrec)
  val appPrec = nextPrec Option.unsafe.get()
  val _of = keyword of "of", nextPrec, currPrec
  val _period = keyword(".", nextPrec, currPrec)
  
  val bracketPrec = nextPrec
  val _leftRound = keyword of "(", bracketPrec, basePrec
  val _rightRound = keyword of ")", basePrec, None
  val _leftCurly = keyword of "{", bracketPrec, basePrec
  val _rightCurly = keyword of "}", basePrec, None
