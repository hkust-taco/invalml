import "./Option.mls"

open Option { Some, None }

module WrappedMap with ...

class WrappedMap with
  let storage = new Map()
  
  set this.(Symbol.iterator) =
    () => storage.(Symbol.iterator)()
  
  fun get(key) =
    if storage.has(key)
    then Some of storage.get(key)
    else None
  
  fun insert(key, value) =
    storage.set(key, value)
    ()
  
  fun updateWith(key)(op) =
    if op(get(key)) is
      Some(value) then storage.set(key, value)
      None then storage.delete(key)
  
  fun keysIterator = storage.keys()
  
  fun valuesIterator = storage.values()
  
  fun values = Array.from of storage.values()

fun toMap(entries) =
  let
    m = empty
    i = 0
    length = entries.length
  while i < length do
    m.insert of ...entries.at(i)
    set i += 1
  m

fun empty = new WrappedMap
