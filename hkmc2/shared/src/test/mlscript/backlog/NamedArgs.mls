:js
:todo


// * Named tuple

[a: 0]
//│ = [{a: 0}]

[1, 2, a: "a", 3, b: "b", 4, 5]
//│ = [1, 2, 3, 4, 5, {a: "a", b: "b"}]

let r = [4, 5, c: "c"]
//│ r = [4, 5, {c: "c"}]

[1, 2, a: "a", 3, b: "b", ...r]
//│ = [1, 2, 3, 4, 5, {c: "c"}, {a: "a", b: "b"}]


let aa = 123
//│ aa = 123

[aa: 0]
//│ = [{aa: 0}]

[aa: aa]
//│ = [{aa: 123}]


// * Named arguments

// :de
:elt
id(a: 0)
//│ Elaborated tree:
//│ Blk:
//│   stats = Nil
//│   res = App:
//│     lhs = SynthSel{member:id}:
//│       prefix = Ref of member:Predef
//│       nme = Ident of "id"
//│     rhs = Tup of Ls of 
//│       Fld:
//│         flags = ()
//│         term = Lit of StrLit of "a"
//│         asc = S of Lit of IntLit of 0
//│ = {a: 0}

id of a: 0
//│ = {a: 0}


print of
  foo: 1
  bar:
    "..."
//│ > {foo: 1, bar: "..."}

print of
  "Hello"
  foo: 1
  bar:
    "..."
  "World"
//│ > Hello World {foo: 1, bar: "..."}




:silent
import "../../mlscript-compile/Iter.mls"
import "../../mlscript-compile/TreeTracer.mls"
let graphics = new Set of tuple of
  "circle", "ellipse", "image", "line", "path"
  "polygon", "polyline", "rect", "text", "use"
let trace = new TreeTracer.TreeTracer
set trace.enabled = true

fun showProps(x, y, props) = Object.entries(props)
  Iter.mapping of case
    ["x", x'] then "x: " + (x + x')
    ["y", y'] then "y: " + (y + y')
    [k, v] then k + ": " + v
  Iter.joined(", ")

fun t(kind, props) =
  if graphics.has(kind) then
    (x, y) => trace.print of kind, " ", showProps(x, y, props)
  else
    (...children) => (x, y) =>
      trace.enter(kind, " ", showProps(x, y, props))
      let x = x + props.("x")
      let y = y + props.("y")
      children Iter.each of child => child(x, y)
      trace.leave(kind)

let display = t("group", x: 0, y: 0) of
  t("circle", x: 10, y: 10, r: 42)
  t("group", x: 20, y: 20) of
    t("circle", x: 30, y: 30, r: 42)
    t("circle", x: 40, y: 40, r: 42)
    t("circle", x: 50, y: 50, r: 42)
    t("circle", x: 60, y: 60, r: 42)
    t("circle", x: 70, y: 70, r: 42)
    t("circle", x: 80, y: 80, r: 42)
//│ display = [function]

display(88, 99)
//│ > ┍ group x: 88, y: 99
//│ > ├ circle x: 98, y: 109, r: 42
//│ > ├─┮ group x: 108, y: 119
//│ > │ ├ circle x: 138, y: 149, r: 42
//│ > │ ├ circle x: 148, y: 159, r: 42
//│ > │ ├ circle x: 158, y: 169, r: 42
//│ > │ ├ circle x: 168, y: 179, r: 42
//│ > │ ├ circle x: 178, y: 189, r: 42
//│ > │ ├ circle x: 188, y: 199, r: 42
//│ > │ ┕ group
//│ > ┕ group
